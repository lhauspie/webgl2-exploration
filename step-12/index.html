<!DOCTYPE html>
<html lang="en">
    <head>
        <meta charset="UTF-8">
        <title>Title</title>
        <script src="../gl-matrix.js"></script>
        <script src="../webgl-obj-loader.js"></script>
        <script src="main.js"></script>




        <script id="skybox-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;

            uniform mat4 uViewMatrix, uProjectionMatrix, uWorldMatrix;

            out vec4 vVertexPosition;

            void main() {
                vVertexPosition = vec4(aVertexPosition, 1.0);

                gl_Position = vec4(aVertexPosition, 1.0);
                //gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);
            }
        </script>

        <script id="skybox-fshader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            in vec4 vVertexPosition;

            uniform samplerCube uSampler;
            uniform mat4 uViewDirectionProjectionInverse;

            out vec4 outColor;

            void main() {
                vec4 t = uViewDirectionProjectionInverse * vVertexPosition;
                outColor = texture(uSampler, normalize(t.xyz / t.w));
            }
        </script>






        <script id="basic-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 1) in vec3 aVertexNormal;
            layout(location = 2) in vec2 aTextureCoord;

            uniform mat4 uNormalMatrix, uViewMatrix, uProjectionMatrix, uWorldMatrix, uTextureMatrix;

            uniform vec3 uAmbientColor;
            uniform vec3 uLightDirection;
            uniform vec3 uLightColor;

            out vec2 vTextureCoord;
            out vec3 vLighting;

            void main(void) {
                gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);
                vTextureCoord = (uTextureMatrix * vec4(aTextureCoord, 0.0, 1.0)).xy;

                highp vec4 transformedNormal = uNormalMatrix * uWorldMatrix * vec4(aVertexNormal, 1.0);

                highp float directional = max(dot(transformedNormal.xyz, uLightDirection), 0.0);
                vLighting = uAmbientColor + (uLightColor * directional);
            }
        </script>

        <script id="basic-fshader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            in vec2 vTextureCoord;
            in vec3 vLighting;

            uniform bool uUseTexture;
            uniform sampler2D uSampler;

            out vec4 outColor;

            void main(void) {
                vec4 texelColor = texture(uSampler, vTextureCoord);
                outColor = vec4(texelColor.rgb * vLighting, texelColor.a);
            }
        </script>






        <script id="reflect-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 1) in vec3 aVertexNormal;

            uniform mat4 uViewMatrix, uProjectionMatrix, uWorldMatrix;

            out vec3 vWorldPosition;
            out vec3 vWorldNormal;

            void main() {
                // Multiply the position by the matrix.
                gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);

                // send the view position to the fragment shader
                vWorldPosition = (uWorldMatrix * vec4(aVertexPosition, 1.0)).xyz;

                // orient the normals and pass to the fragment shader
                vWorldNormal = mat3(uWorldMatrix) * aVertexNormal;
            }
        </script>

        <script id="reflect-fshader" type="x-shader/x-fragment">#version 300 es
            precision highp float;

            in vec3 vWorldPosition;
            in vec3 vWorldNormal;

            uniform samplerCube uSampler;
            // The position of the camera
            uniform vec3 uCameraPosition;
            uniform float uAlpha;

            out vec4 outColor;

            void main() {
                vec3 worldNormal = normalize(vWorldNormal);
                vec3 eyeToSurfaceDir = normalize(vWorldPosition - uCameraPosition);
                vec3 direction = reflect(eyeToSurfaceDir, worldNormal);

                outColor = texture(uSampler, direction);
                outColor.a = uAlpha;
            }
        </script>






        <script id="refract-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 1) in vec3 aVertexNormal;

            uniform mat4 uViewMatrix, uProjectionMatrix, uWorldMatrix, uNormalMatrix;
            uniform vec3 uCameraPosition;

            // Indices of refraction
            const float Air = 1.0;
            const float Glass = 1.51714;

            // Air to glass ratio of the indices of refraction (Eta)
            const float Eta = Air / Glass;

            // see http://en.wikipedia.org/wiki/Refractive_index Reflectivity
            const float R0 = ((Air - Glass) * (Air - Glass)) / ((Air + Glass) * (Air + Glass));

            out vec3 vReflection;
            out vec3 vRefraction;
            out float vFresnel;

            void main() {
                vec4 vertex = uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);

                vec3 incident = normalize(vec3(vertex) - uCameraPosition);

                // Assume incoming normal is normalized.
                vec3 normal = vec3(uNormalMatrix * vec4(aVertexNormal, 1.0));

                vRefraction = refract(incident, normal, Eta);
                vReflection = reflect(incident, normal);

                // see http://en.wikipedia.org/wiki/Schlick%27s_approximation
                vFresnel = R0 + (1.0 - R0) * pow((1.0 - dot(-incident, normal)), 5.0);

                gl_Position = uProjectionMatrix * vertex;
            }
        </script>

        <script id="refract-fshader" type="x-shader/x-fragment">#version 300 es
            precision highp float;

            uniform samplerCube uSampler;
            uniform float uAlpha;

            in vec3 vRefraction;
            in vec3 vReflection;
            in float vFresnel;

            out vec4 outColor;

            void main(void)
            {
                vec4 refractionColor = texture(uSampler, normalize(vRefraction));
                vec4 reflectionColor = texture(uSampler, normalize(vReflection));

                outColor = mix(refractionColor, reflectionColor, vFresnel);
                outColor.a = uAlpha;
            }
        </script>






        <script id="phong-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 1) in vec3 aVertexNormal;
            layout(location = 2) in vec2 aTextureCoord;

            uniform mat4 uNormalMatrix, uViewMatrix, uProjectionMatrix, uWorldMatrix, uTextureMatrix;

            out vec3 vNormal;
            out vec3 vPosition;
            out vec2 vTextureCoord;

            void main() {
                vec4 vPosition4 = uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);
                vPosition = vec3(vPosition4) / vPosition4.w;
                vNormal = vec3(uNormalMatrix * uWorldMatrix * vec4(aVertexNormal, 0.0));

                vTextureCoord = (uTextureMatrix * vec4(aTextureCoord, 0., 1.)).xy;
                gl_Position = uProjectionMatrix * vPosition4;
            }
        </script>

        <script id="phong-fshader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;
            out vec4 fragColor;

            uniform vec3 uLightPos;
            uniform float uShininess;
            uniform float uKa;
            uniform float uKd;
            uniform float uKs;
            uniform vec3 uAmbientColor;
            uniform vec3 uDiffuseColor;
            uniform vec3 uSpecularColor;
            uniform float uAlpha;

            uniform sampler2D uSampler;
            uniform sampler2D uSpecularSampler;
            uniform bool uUseTexture;
            uniform bool uUseLight;

            in vec3 vNormal;
            in vec3 vPosition;
            in vec2 vTextureCoord;

            void main(void) { // http://evasion.imag.fr/Enseignement/cours/2012/SIA/Shaders/tp1.html
                vec3 N = normalize(vNormal);
                vec3 L = normalize(uLightPos - vPosition);

                // Lambert's cosine law
                float lambertian = max(dot(N, L), 0.0);
                float specular = 0.0;
                if (lambertian > 0.0) {
                    vec3 R = reflect(-L, N);      // Reflected light vector
                    vec3 V = normalize(-vPosition); // Vector to viewer
                    // Compute the specular term
                    float specAngle = max(dot(R, V), 0.0);
                    specular = pow(specAngle, uShininess);
                }

                vec4 texelColor = texture(uSampler, vTextureCoord);
                vec4 specularColor = texture(uSpecularSampler, vTextureCoord);
                fragColor = vec4(uKa * vec4(uAmbientColor, 1.0) +
                                uKd * lambertian * texelColor +
                                uKs * specular * vec4(uSpecularColor, 1.0) * specularColor);
                //fragColor = specularColor;
              }
        </script>






        <script id="toon-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 1) in vec3 aVertexNormal;
            layout(location = 2) in vec2 aTextureCoord;

            //uniform mat4 uNormalMatrix, uViewMatrix, uProjectionMatrix, uCameraMatrix;
            uniform mat4 uViewMatrix, uProjectionMatrix, uWorldMatrix;

            out vec3 vWorldPosition;
            out vec3 vWorldNormal;

            void main() {
                // Multiply the position by the matrix.
                gl_Position = uProjectionMatrix * uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0);

                // send the view position to the fragment shader
                vWorldPosition = (uViewMatrix * uWorldMatrix * vec4(aVertexPosition, 1.0)).xyz;

                // orient the normals and pass to the fragment shader
                vWorldNormal = normalize(mat3(uViewMatrix * uWorldMatrix) * aVertexNormal);
            }
        </script>

        <script id="toon-fshader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;
            out vec4 outColor;

            uniform vec3 uLightPos;
            uniform vec3 uCameraPosition;
            uniform float uShininess;
            uniform float uKd;
            uniform float uKs;
            uniform vec3 uAmbientColor;
            uniform vec3 uDiffuseColor;
            uniform float uAlpha;

            in vec3 vWorldPosition;
            in vec3 vWorldNormal;

            //number of levels
            //for diffuse color
            const float levels = 3.0;
            const float scaleFactor = 1.0 / levels;

            void main() {
                vec3 L = normalize(uLightPos - vWorldPosition);
                vec3 V = normalize(uCameraPosition - vWorldPosition);

                float diffuse = max(0.0, dot(L, vWorldNormal));
                vec3 diffuseColor = uDiffuseColor * uKd * floor(diffuse * levels) * scaleFactor;

                vec3 H = normalize(L + V);

                float specular = 0.0;
                if (dot(L, vWorldNormal) > 0.0) {
                   specular = uKs * pow( max(0.0, dot( H, vWorldNormal)), uShininess);
                }

                //limit specular
                float specMask = (pow(dot(H, vWorldNormal), uShininess) > 0.4) ? 1.0 : 0.0;

                float edgeDetection = (dot(V, vWorldNormal) > 0.3) ? 1.0 : 0.0;

                vec3 color = edgeDetection * (uAmbientColor + diffuseColor + specular * specMask);
                //vec3 color = vec3(1.0, 1.0, 1.0);

                outColor = vec4(color, uAlpha);
            }
        </script>






        <script id="background-vshader" type="x-shader/x-vertex">#version 300 es
            layout(location = 0) in vec3 aVertexPosition;
            layout(location = 2) in vec2 aTextureCoord;

            uniform mat4 uNormalMatrix;
            uniform mat4 uViewMatrix;
            uniform mat4 uProjectionMatrix;

            out vec2 vTextureCoord;

            void main(void) {
                gl_Position = uProjectionMatrix  * vec4(aVertexPosition, 1.0);
                vTextureCoord = aTextureCoord;
            }
        </script>

        <script id="background-fshader" type="x-shader/x-fragment">#version 300 es
            precision mediump float;

            in vec2 vTextureCoord;

            uniform sampler2D uSampler;

            out vec4 outColor;

            void main(void) {
                outColor = texture(uSampler, vTextureCoord);
            }
        </script>
    </head>
    <body onload="main()">
        <canvas id="canvas" style="background: black">
            It should be a very cool 3D animation here, but your dinosaur does not support canvas tag. <br/>
            It's time to come back to the present days... :(
        </canvas>

        <div id="noContextLayer" style="display: none">
            <h2>Ouch, your dinosaur does not support WebGL2 yet. </h2>
            <h3>It's time to come back to the present days...</h3>
        </div>
    </body>
</html>